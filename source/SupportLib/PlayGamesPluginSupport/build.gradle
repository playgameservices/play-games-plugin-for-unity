apply plugin: 'maven'
apply plugin: 'com.android.library'

group 'com.google.games'
project.ext.baseName = "gpgs-plugin-support"

if (project.hasProperty("projVersion")) {
    project.version = project.projVersion
} else {
    project.version '0-SNAPSHOT'
}

if (project.hasProperty("uploadDir")) {
    project.ext.uploadDir = project.uploadDir
} else {
    project.ext.uploadDir = "${buildDir}/m2repository"
}

if (project.hasProperty("gpgSdkDir")) {
    project.ext.gpgSdkDir = project.gpgSdkDir
} else {
    project.ext.gpgSdkDir = "${projectDir}/gpg-cpp-sdk/android"
}

android {
    // TODO(claywilkinson): Clean up lint warnings
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        versionName  project.version
        archivesBaseName = project.ext.baseName
        minSdkVersion 14

        ndk.abiFilters 'x86', 'armeabi-v7a'


        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -Wall -Werror"
                arguments "-DGPG_SDK_PATH=${gpgSdkDir}",
                        "-DANDROID_STL=c++_static",
                        "-DANDROID_TOOLCHAIN=clang"
            }
        }
    }
    compileSdkVersion 27
    externalNativeBuild {
        cmake.path "CMakeLists.txt"
    }
}

// Rename the .aar file to be .srcaar to work around
// // having the local repo in a subdirectory in a Unity project.
// // The jar resolver plugin handles the changing back from .srcaar
// // to .aar
// //
// // This is done in 2 parts, first declare the .srcaar as an
// // artifact, the second is a task to rename the aar -> srcaar.
//
artifacts {
    archives file("build/${baseName}-${version}.srcaar")
}


task renameLibrary() {
  doFirst {
    copy {
      from "${buildDir}/outputs/aar/${baseName}-release.aar"
     into "${buildDir}"
      rename {
        String fn ->
          return fn.replace("release.aar", "${version}.srcaar")
      }
    }
  }


  doLast {
    configurations.archives.with   {
         artifacts.remove artifacts.find { it.extension == "aar"}
      }
  }
}

tasks.whenTaskAdded { task ->
  if (task.name == 'assembleRelease')
    renameLibrary.dependsOn task
}

uploadArchives {
  dependsOn renameLibrary
  repositories {
    mavenDeployer {
      repository(url: uri(project.ext.uploadDir))
    }
  }
}

dependencies {
    implementation 'com.google.android.gms:play-services-games:11.6+'
    implementation 'com.google.android.gms:play-services-auth:11.6+'
    implementation 'com.google.android.gms:play-services-nearby:11.6+'
}
