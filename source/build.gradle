/*
*   Gradle file to build the Unity plugin for Google Play Game Services.
*/

buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
}
/*
    Project level variables
*/
project.ext {
    os_name = System.getProperty("os.name").toLowerCase();
    os_osx = os_name.contains("mac os x");
    os_windows = os_name.contains("windows");
    os_linux = os_name.contains("linux");

    ndk_root = System.getProperty("ANDROID_NDK_ROOT")
    if (ndk_root == null || ndk_root.isEmpty()) {
        ndk_root = System.getenv("ANDROID_NDK_ROOT")
    }
    sdk_root = System.getProperty("ANDROID_HOME")
    if (sdk_root == null || sdk_root.isEmpty()) {
        sdk_root = System.getenv("ANDROID_HOME")
    }
    unity_exe = System.getProperty("UNITY_EXE")
    if (unity_exe == null || unity_exe.isEmpty()) {
        unity_exe = System.getenv("UNITY_EXE")
    }

    // Take a guess at where Unity is located as a last resort.
    if (unity_exe == null || unity_exe.isEmpty()) {
      if (os_osx) {
        unity_exe ='/Applications/Unity/Unity.app/Contents/MacOS/Unity'
      } else if (os_windows) {
        unity_exe ='c:\\program files\\unity\\editor\\unity.exe'
      } else {
        unity_exe = 'Unity'
      }
    }

    git_exe = System.getProperty("GIT_EXE")
    if (git_exe == null || git_exe.isEmpty()) {
        git_exe = System.getenv("GIT_EXE")
    }
    if (git_exe == null || git_exe.isEmpty()) {
        git_exe = 'git'
    }

    pluginSrc = file('PluginDev').absolutePath
    pluginBuildSrc = file('build/PluginSrc').absolutePath
    pluginProj = file('build/PluginProject').absolutePath
    buildPath = file('build').absolutePath
    samplesPath = file('../samples').absolutePath
    samplesBuildSrc = file('build/sampleSrc').absolutePath
    exportPath = file('build/plugin.unitypackage').absolutePath
    currentPluginPath = file('../current-build').absolutePath
    currentPluginVersion = "0.9.50"
    currentPluginBasename = 'GooglePlayGamesPlugin'
    currentPluginName = "${currentPluginBasename}-${currentPluginVersion}.unitypackage"
    resolverDir = new File('build/jarresolver').absolutePath

    editorDir = "Assets/GooglePlayGames/Editor"
    pluginRepoDir = "${pluginProj}/${editorDir}/m2repository"

    sampleList = ['CubicPilot', 'Minimal',
         'QuizRacer',  'TicTacToe',
         'NearbyDroids', 'SmokeTest']

    gpg_sdk_link = System.getProperty("GPG_SDK_URL")
    if (gpg_sdk_link == null || gpg_sdk_link.isEmpty()) {
        gpg_sdk_link = System.getenv("GPG_SDK_URL")
    }
    if (gpg_sdk_link == null || gpg_sdk_link.isEmpty()) {
      gpg_sdk_link = 'https://developers.google.com/games/services/downloads/gpg-cpp-sdk.v3.0.zip'
    }

    jarresolver_uri = System.getProperty("RESOLVER_PACKAGE_URI")
    jarresolver_repos = [
      'https://github.com/googlesamples/unity-jar-resolver.git'
    ]
    jarresolver_tag = null;
}

if (!tasks.findByName('prebuild')) {
    task prebuild() {
        println('Local prebuild')
    }
    prebuild.dependsOn {
        project.tasks.findAll { task -> task.ext.has('remoteTaskPhase') && task.ext.remoteTaskPhase == 'prebuild' }
    }
}
if (!tasks.findByName('build')) {
    task build() {
        println('Local build')
    }
    build.dependsOn {
        project.tasks.findAll { task -> task.ext.has('remoteTaskPhase') && task.ext.remoteTaskPhase == 'build' }
    }
}
if (!tasks.findByName('postbuild')) {
    task postbuild() {
        println('Local postbuild')
    }
    postbuild.dependsOn {
        project.tasks.findAll { task -> task.ext.has('remoteTaskPhase') && task.ext.remoteTaskPhase == 'postbuild' }
    }
}

project.defaultTasks = ['prebuild', 'build', 'postbuild']


/**
    Final task of building all the unity packages.
**/
task unity_package(dependsOn:'export_package') {

    description = "Top level task for building the unity packages"

    doLast {
        println "Packaging Complete!"
    }

    // Mark this a build phase task for remoteTask support.
    ext.remoteTaskPhase = 'build'

    // Depends on packaging all the samples
    dependsOn {
        tasks.findAll { task -> task.name.startsWith('PackageSample') }
    }
}

/**
    Generate tasks for the samples.  There are several tasks for each sample.
**/
sampleList.each { sampleName ->
    def createTask   = project.tasks.create('CreateSample' + sampleName)
    def copySrcTask   = project.tasks.create('CopySrcSample' + sampleName)
    def copyBuildSrcTask   = project.tasks.create('CopyBuildSrcSample' + sampleName)

    // this is the path to the source code
    def sampleSrcDir = samplesPath + '/' + sampleName + '/Source/Assets/' + sampleName

    // this is the path to the source code in the build dir
    def sampleBuildSrcDir = samplesBuildSrc + '/' + sampleName + '/Source/Assets/' + sampleName

    // this is the path to build the sample package
    def sampleDestSrcDir = buildPath + '/Samples/' + sampleName + '/Assets/' + sampleName

    // the relative path to the sample assets within the project.
    def sampleAssets = "Assets/${sampleName}"

    def sampleDir = file("${buildPath}/Samples")
    if (!sampleDir.exists()) {
        sampleDir.mkdirs()
    }

    /*
        Create the sample in the build directory
    */
    createTask.description = "Create the ${sampleName} package"
    createTask.dependsOn 'export_package'
    createTask.doFirst {
        delete "${buildPath}/Samples/${sampleName}"

        def logFilename = "${buildPath}/CreateSample${sampleName}.log"
        def argv = [
          "-gvh_disable",
          "-g.building",
          "-batchmode",
          "-createProject", "${buildPath}/Samples/${sampleName}",
          "-importPackage", "${exportPath}",
          "-logFile", logFilename,
          "-quit"
        ]
        runUnity(argv, logFilename)

    }
    // This is a build task
    createTask.ext.remoteTaskPhase = 'build'

    /*
        Copy the source of the sample into build dir.
        This is done so it can be remoted and assembled as a part of a RemoteTask.
    */
    copyBuildSrcTask.description = "Copies sample ${sampleName} source code to build area"
    copyBuildSrcTask.doFirst {
            copy {
                from sampleSrcDir
                into sampleBuildSrcDir
            }
    }
    copyBuildSrcTask.doLast {
            println 'copied ' + sampleSrcDir + ' to ' + sampleBuildSrcDir
    }

    copyBuildSrcTask.ext.remoteTaskPhase = 'prebuild'
    /*
        Copy the source of the sample into build target.  Don't import the
        sample package, since we want the latest source code.
    */
    copySrcTask.description = "Copies sample ${sampleName} source code to sample project build area"
    copySrcTask.dependsOn createTask, copyBuildSrcTask
    copySrcTask.doFirst {
            copy {
                from sampleBuildSrcDir
                into sampleDestSrcDir
            }
    }
    copySrcTask.doLast {
            println 'copied ' + sampleBuildSrcDir + ' to ' + sampleDestSrcDir
    }

    copySrcTask.ext.remoteTaskPhase = 'build'

    /*
        Create the exported package
    */
    def packageTask   = project.tasks.create('PackageSample' + sampleName)

        packageTask.description = "Package ${sampleName} sample"

        packageTask.dependsOn createTask, copySrcTask

        packageTask.doLast {
            def logFilename = "${buildPath}/ExportSample${sampleName}.log"
            def argv = [
              "-gvh_disable",
              "-g.building",
              "-batchmode",
              "-projectPath", "${buildPath}/Samples/${sampleName}",
              "-buildTarget", "android",
              "-logFile", logFilename,
              "-exportPackage", "${sampleAssets}",
                    "${buildPath}/Samples/${sampleName}.unitypackage",
              "-quit"
              ]
            runUnity(argv, logFilename)
        }
        packageTask.ext.remoteTaskPhase = 'build'

        /*
            Finally, copy the package to the source sample dir.
        */
        def copySamplePackageTask = project.tasks.create('CopySamplePackage' + sampleName)
        copySamplePackageTask.description = "Copy unity package for " + sampleName + " back to src dir"
        copySamplePackageTask.doLast {
            copy {
                from file("${buildPath}/Samples/${sampleName}.unitypackage")
                into file("../samples/${sampleName}")
            }
        }

        copySamplePackageTask.ext.remoteTaskPhase = 'postbuild'
}

task import_jarresolver() {
    description = "Imports the jar resolver package into the plugin"
    ext.remoteTaskPhase = 'build'
    doFirst {
        if (file(pluginProj).exists()) {
            delete pluginProj
        }
    }
    doLast {
        def tree = fileTree("${resolverDir}")
            {
                include '*.unitypackage'
            }
        def jarresolver_package = tree.getSingleFile()

        def logFilename = "${buildDir}/import_resolver_unity.log"
        def argv = [
            "-gvh_disable",
            "-g.building",
            "-batchmode",
            "-createProject", "${pluginProj}",
            "-logFile", logFilename,
            "-importPackage", "${jarresolver_package}",
            "-quit"
            ]
        runUnity(argv, logFilename)
    }

}
// don't explicitly depend on copy_jarresolver here since it is run
// as a prebuild step, and import_jarresolver is a build step which can happen
// on a machine with limited network connectivity.
// import_jarresolver.dependsOn "copy_jarresolver"

task copy_pluginProjSrc () {
    doFirst {
      copy {
        from {"${pluginBuildSrc}"}
        includes ["**/*"]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        into {"${pluginProj}"}
      }
    }
    doLast {
      copy {
        from {"${buildDir}/m2repository"}
        includes ["**/*"]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        into {"${pluginRepoDir}"}
      }
    }
}
copy_pluginProjSrc.dependsOn import_jarresolver
copy_pluginProjSrc.ext.remoteTaskPhase = 'build'



task generate_dependency_xml() {
  description "Generate the JarResolver dependency file for the support library"
  dependsOn copy_pluginProjSrc
  doFirst {
    def xmlFile = file("${pluginProj}/${editorDir}/${currentPluginBasename}Dependencies.xml")
    xmlFile << '<?xml version="1.0" encoding="UTF-8" ?>\n'
    xmlFile << '<dependencies>\n'
    xmlFile << '<!-- Internal library dependency generated at build time. \n'
    xmlFile << '    It also defines the transitive dependencies on play-services\n'
    xmlFile << '-->\n'
    xmlFile << '  <androidPackages>\n'
    xmlFile << '    <androidPackage spec="com.google.games:gpgs-plugin-support:'
    xmlFile << "${currentPluginVersion}\">\n"
    xmlFile << '      <repositories>\n'
    xmlFile << '        <repository>Assets/GooglePlayGames/Editor/m2repository</repository>\n'
    xmlFile << '      </repositories>\n'
    xmlFile << '    </androidPackage>\n'
    xmlFile << '  </androidPackages>\n'
    xmlFile << '</dependencies>\n'
  }
}


task inject_versionIntoMetaFiles() {
  description 'Inject the version number into the plugin\'s meta files.'
  dependsOn copy_pluginProjSrc, generate_dependency_xml, import_jarresolver
  doLast {
    def currentManifestFile = versionedAssetName("${currentPluginBasename}.txt", "${currentPluginVersion}")
    for (fileobj in fileTree("${pluginProj}")) {
      if (fileobj.path.endsWith('.meta')) {
        // Skip the manifest files for any previous versions
        if (fileobj.path.contains("Editor/${currentPluginBasename}") &&
            !fileobj.path.contains(currentManifestFile)) {
          continue
        }
        def lines = fileobj.text.split('\n')
        def outputLines = []
        def added = false
        for (line in lines) {
          outputLines.add(line)
          if (line.contains('labels:')) {
            outputLines.add("- gvh_v${currentPluginVersion}")
            added = true
          } else if (line.contains('folderAsset:') && line.contains('yes')) {
            added = true
          }
        }
        if (!added) {
          outputLines.add("labels:\n- gvh_v${currentPluginVersion}")
        }
        fileobj.write(outputLines.join('\n') + '\n')
      }
    }
  }
}

task generate_manifest(dependsOn: [ 'inject_versionIntoMetaFiles']) {
  description 'Generate a manifest for the files in the plug-in.'
  doLast {
    def dir = file("${pluginProj}/Assets")
    def list = []
    dir.eachFileRecurse(groovy.io.FileType.FILES) { filename ->
        def path = filename.path
        if (!path.toLowerCase().endsWith('.meta')) {
          list << filename.path.replace("${pluginProj}/", '')
        }
    }
    def manifest = file("${pluginProj}/${editorDir}/" +
          versionedAssetName(currentPluginBasename + '.txt', "${currentPluginVersion}"))
    manifest.write(list.join('\n') + '\n')
  }
}


task copy_manifestMetadata(dependsOn: generate_manifest, type: Copy) {
  def manifestBasename = versionedAssetName(currentPluginBasename + '.txt',
        null) + '.meta'
  description 'Copy .meta file for the plugin manifest.'
  from file("${pluginProj}/${editorDir}/" + manifestBasename)
  into file("${pluginProj}/${editorDir}/")
  rename {
    String fn ->
      return fn.replace(manifestBasename,
          versionedAssetName(currentPluginBasename + '.txt',
          "${currentPluginVersion}") + '.meta')
  }
}
task export_package () {
    description = "Creates and exports the Plugin unity package"
    doFirst {
        ext.path = "${pluginProj}/Assets/GooglePlayGames/Editor/projsettings.txt"
        delete '${ext.path}'
    }

    doLast {
        def logFilename = "${buildDir}/unity.log"
        def argv = [
                "-g.building",
                // This prevents the VersionHandler to preserve the plugin layout
                "-gvh_disable",
                // NOTE: This doesn't target Android since we don't want to have
                // dependencies on the Jar Resolver as they require the plugin to
                // be enabled during the build process which would break the
                // versioning process.  Compilation is verified when exporting
                // the sample projects.
                "-batchmode",
                "-projectPath", "${pluginProj}",
                "-logFile", logFilename,
                "-exportPackage",
                    "Assets/GooglePlayGames",
                    "Assets/GooglePlayGames/Plugins/Android/GooglePlayGamesManifest.plugin",
                    "Assets/PlayServicesResolver",
                "${exportPath}",
                "-quit"
        ]
        runUnity(argv, logFilename)
    }
}
export_package.dependsOn generate_manifest, import_jarresolver, copy_manifestMetadata
export_package.dependsOn copy_pluginProjSrc
export_package.ext.remoteTaskPhase = 'build'
/*
    Copy the plugin to the current-build directory
*/
task copy_plugin() {
    description = 'Copy plugin to the current-build directory'
    doFirst {
            copy {
                from file(exportPath)
                into file(currentPluginPath)
                rename ('plugin.unitypackage', currentPluginName)
            }
        }

    doLast {
        println "Copied  ${exportPath}  to   ${currentPluginPath}"
    }

    ext.remoteTaskPhase = 'postbuild'
}

task get_gpg_cpp_sdk {

    doFirst {
        ant.get(src: gpg_sdk_link, dest: "${buildDir}/gpg_cpp_sdk.zip", skipexisting: 'true')
    }
    doLast {
        println "Got ${gpg_sdk_link} as ${buildDir}/gpg_cpp_sdk.zip"
    }
}
get_gpg_cpp_sdk.description = "Download the gpg sdk from the specified location"

get_gpg_cpp_sdk.ext.remoteTaskPhase = 'prebuild'

task copy_plugin_source {
  description = "Copies plugin source into build directory"
  ext.remoteTaskPhase = 'prebuild'

  doFirst {
    copy {
        from {"${pluginSrc}"}
        into {"${pluginBuildSrc}"}
    }
  }
}

task copy_jarresolver() {
    description = "Clones the jar resolver project and copied into the build."
    ext.remoteTaskPhase = "prebuild"
    dependsOn copy_plugin_source

    doFirst {
        if (file(resolverDir).exists()) {
            delete resolverDir
        }
    }

    doLast {
        if (jarresolver_uri != null) {
            mkdir("${resolverDir}")
            def resolver = new File("${resolverDir}/resolver.unitypackage")
            new URL("${jarresolver_uri}").withInputStream {
                inputStream -> resolver.withOutputStream { it << inputStream }
            }
            return
        }

        for (repo in jarresolver_repos) {
            println 'clone ' + repo
            def result = exec {
                executable "${git_exe}"
                args "clone", repo, "${resolverDir}"
                ignoreExitValue true
            }
            if (result.exitValue == 0 && jarresolver_tag != null) {
                result = exec {
                    executable "${git_exe}"
                    args "checkout", "-b", "buildver", "${jarresolver_tag}"
                    workingDir "${resolverDir}"
                }
            }

            if (result.exitValue == 0) {
                println "Downloaded resolver from " + repo
                return
            }
        }
    }
}

task copy_gpg_sdk {
  description = "Unzips the GPG SDK into the correct dir for NDK"
  dependsOn get_gpg_cpp_sdk
  ext.remoteTaskPhase = 'prebuild'
  doFirst {
    if(os_linux || os_osx) {
            def result = exec {
                executable "unzip"
                args "${buildDir}/gpg_cpp_sdk.zip", "-d", "${buildDir}/gpg_sdk"
            }
    } else {
        copy {
            from zipTree("${buildDir}/gpg_cpp_sdk.zip")
            into {"${buildDir}/gpg_sdk"}
        }
    }
  }
}

task build_java_support_lib(type: GradleBuild) {
    description = "Builds the client libraries for Android"
    buildFile = 'SupportLib/build.gradle'
    startParameter.projectProperties.put('projVersion',currentPluginVersion)
    startParameter.projectProperties.put('uploadDir', "${buildDir}/m2repository")
    startParameter.projectProperties.put('gpgSdkDir', "${buildDir}/gpg_sdk/gpg-cpp-sdk/android")

    tasks = ['build', 'uploadArchives']
}
build_java_support_lib.ext.remoteTaskPhase = 'prebuild'
build_java_support_lib.dependsOn copy_gpg_sdk

task clean_support_lib(type: GradleBuild) {
    description = "cleans the client libraries for Android"
    buildFile = 'SupportLib/build.gradle'
    tasks = ['clean']
}
task clean() {
    doFirst {
        delete 'build'
        }
    dependsOn clean_support_lib
}


// Construct the name of a versioned asset from the source filename and version
// // string.
// // The encoded string takes the form...
// // ${filename}_v${version}_.${extension}
// // where extension is derived from the specified filename.
def versionedAssetName(filename, version) {
  def extensionIndex = filename.lastIndexOf('.')
  def basename = filename.substring(0, extensionIndex)
  def extension = filename.substring(extensionIndex)
  // Encode the DLL version and target names into the DLL in the form...
  // ${dllname}_t${hypen_separated_target_names}_v${version}.dll
  def targetName = basename
  if (version != null && !version.isEmpty()) {
    targetName += '_v' + version
  }
  return targetName + extension
}

def runUnity(argv, logFilename) {
  def result = exec {
    executable "${project.ext.unity_exe}"
    args argv
    ignoreExitValue true
  }

  if (result.exitValue != 0) {
    println "***Error Running Unity: ${result}"
    def src = file(logFilename).text
    print src
    throw new GradleException('error exporting sample')
  }
}
