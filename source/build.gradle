/*
*   Gradle file to build the Unity plugin for Google Play Game Services.
*/

buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
}
/*
    Project level variables
*/
project.ext {
    ndk_root = System.getProperty("ANDROID_NDK_ROOT")
    if (ndk_root == null || ndk_root.isEmpty()) {
        ndk_root = System.getenv("ANDROID_NDK_ROOT")
    }
    sdk_root = System.getProperty("ANDROID_HOME")
    if (sdk_root == null || sdk_root.isEmpty()) {
        sdk_root = System.getenv("ANDROID_HOME")
    }
    unity_exe = System.getProperty("UNITY_EXE")
    if (unity_exe == null || unity_exe.isEmpty()) {
        unity_exe = System.getenv("UNITY_EXE")
    }
    if (unity_exe == null || unity_exe.isEmpty()) {
        unity_exe ='/Applications/Unity/Unity.app/Contents/MacOS/Unity'
    }

    git_exe = System.getProperty("GIT_EXE")
    if (git_exe == null || git_exe.isEmpty()) {
        git_exe = System.getenv("GIT_EXE")
    }
    if (git_exe == null || git_exe.isEmpty()) {
        git_exe = 'git'
    }

    pluginSrc = file('PluginDev').absolutePath
    pluginBuildSrc = file('build/PluginSrc').absolutePath
    pluginProj = file('build/PluginProject').absolutePath
    buildPath = file('build').absolutePath
    samplesPath = file('../samples').absolutePath
    samplesBuildSrc = file('build/sampleSrc').absolutePath
    exportPath = file('build/plugin.unitypackage').absolutePath
    currentPluginPath = file('../current-build').absolutePath
    currentPluginVersion = "0.9.42"
    currentPluginName = 'GooglePlayGamesPlugin-' + currentPluginVersion + '.unitypackage'
    resolverDir = new File('build/jarresolver').absolutePath

    sampleList = ['CubicPilot', 'Minimal',
         'QuizRacer',  'TicTacToe',
         'NearbyDroids', 'SmokeTest', 'TrivialQuest']

    gpg_sdk_link = 'https://developers.google.com/games/services/downloads/gpg-cpp-sdk.v2.3.zip'

    jarresolver_uri = System.getProperty("RESOLVER_PACKAGE_URI")
    jarresolver_repos = [
      'https://github.com/googlesamples/unity-jar-resolver.git'
    ]
     jarresolver_tag = null;
}


if (!tasks.findByName('prebuild')) {
    task prebuild() {
        println('Local prebuild')
    }
    prebuild.dependsOn {
        project.tasks.findAll { task -> task.ext.has('remoteTaskPhase') && task.ext.remoteTaskPhase == 'prebuild' }
    }
}
if (!tasks.findByName('build')) {
    task build() {
        println('Local build')
    }
    build.dependsOn {
        project.tasks.findAll { task -> task.ext.has('remoteTaskPhase') && task.ext.remoteTaskPhase == 'build' }
    }
}
if (!tasks.findByName('postbuild')) {
    task postbuild() {
        println('Local postbuild')
    }
    postbuild.dependsOn {
        project.tasks.findAll { task -> task.ext.has('remoteTaskPhase') && task.ext.remoteTaskPhase == 'postbuild' }
    }
}

project.defaultTasks = ['prebuild', 'build', 'postbuild']


/**
    Final task of building all the unity packages.
**/
task unity_package(dependsOn:'export_package') {

    description = "Top level task for building the unity packages"

    doLast {
        println "Packaging Complete!"
    }

    // Mark this a build phase task for remoteTask support.
    ext.remoteTaskPhase = 'build'

    // Depends on packaging all the samples
    dependsOn {
        tasks.findAll { task -> task.name.startsWith('PackageSample') }
    }
}

/**
    Generate tasks for the samples.  There are several tasks for each sample.
**/
sampleList.each { sampleName ->
    def createTask   = project.tasks.create('CreateSample' + sampleName)
    def copySrcTask   = project.tasks.create('CopySrcSample' + sampleName)
    def copyBuildSrcTask   = project.tasks.create('CopyBuildSrcSample' + sampleName)

    // this is the path to the source code
    def sampleSrcDir = samplesPath + '/' + sampleName + '/Source/Assets/' + sampleName

    // this is the path to the source code in the build dir
    def sampleBuildSrcDir = samplesBuildSrc + '/' + sampleName + '/Source/Assets/' + sampleName

    // this is the path to build the sample package
    def sampleDestSrcDir = buildPath + '/Samples/' + sampleName + '/Assets/' + sampleName

    // the relative path to the sample assets within the project.
    def sampleAssets = "Assets/${sampleName}"

    def sampleDir = file("${buildPath}/Samples")
    if (!sampleDir.exists()) {
        sampleDir.mkdirs()
    }

    /*
        Create the sample in the build directory
    */
    createTask.description = "Create the ${sampleName} package"
    createTask.dependsOn 'export_package'
    createTask.doFirst {
        delete "${buildPath}/Samples/${sampleName}"

        exec {
            executable "${unity_exe}"
            args "-batchmode",
                 "-createProject", "${buildPath}/Samples/${sampleName}",
                 "-logFile", "${buildPath}/CreateSample${sampleName}.1.log",
                 "-importPackage", "${exportPath}",
                 "-quit"
        }
        // Activate the resolver.
        exec {
            executable "${unity_exe}"
             args "-batchmode",
                 "-projectPath", "${buildPath}/Samples/${sampleName}",
                 "-logFile", "${buildPath}/CreateSample${sampleName}.2.log",
                 "-executeMethod", "Google.VersionHandler.UpdateNow",
                 "-quit"
        }
    }
    // This is a build task
    createTask.ext.remoteTaskPhase = 'build'

    /*
        Copy the source of the sample into build dir.
        This is done so it can be remoted and assembled as a part of a RemoteTask.
    */
    copyBuildSrcTask.description = "Copies sample ${sampleName} source code to build area"
    copyBuildSrcTask.doFirst {
            copy {
                from sampleSrcDir
                into sampleBuildSrcDir
            }
    }
    copyBuildSrcTask.doLast {
            println 'copied ' + sampleSrcDir + ' to ' + sampleBuildSrcDir
    }

    copyBuildSrcTask.ext.remoteTaskPhase = 'prebuild'
    /*
        Copy the source of the sample into build target.  Don't import the
        sample package, since we want the latest source code.
    */
    copySrcTask.description = "Copies sample ${sampleName} source code to sample project build area"
    copySrcTask.dependsOn createTask, copyBuildSrcTask
    copySrcTask.doFirst {
            copy {
                from sampleBuildSrcDir
                into sampleDestSrcDir
            }
    }
    copySrcTask.doLast {
            println 'copied ' + sampleBuildSrcDir + ' to ' + sampleDestSrcDir
    }

    copySrcTask.ext.remoteTaskPhase = 'build'

    /*
        Create the exported package
    */
    def packageTask   = project.tasks.create('PackageSample' + sampleName)

        packageTask.description = "Package ${sampleName} sample"

        packageTask.dependsOn createTask, copySrcTask
        /*
                Next, export the project
                Target Android here since the plugin only works for
                android or ios, otherwise the sample won't compile
                and then can't be exported.
        */

        packageTask.doLast {
            exec {
                executable "${unity_exe}"
                args "-batchmode",
                    // This prevents the VersionHandler to preserve the plugin layout
                    "-gvh_disable",
                     "-buildTarget", "android",
                     "-projectPath", "${buildPath}/Samples/${sampleName}",
                     "-logFile", "${buildPath}/ExportSample${sampleName}.log",
                     "-exportPackage", "${sampleAssets}",
                         "${buildPath}/Samples/${sampleName}.unitypackage",
                     "-quit"
            }
        }
        packageTask.ext.remoteTaskPhase = 'build'

        /*
            Finally, copy the package to the source sample dir.
        */
        def copySamplePackageTask = project.tasks.create('CopySamplePackage' + sampleName)
        copySamplePackageTask.description = "Copy unity package for " + sampleName + " back to src dir"
        copySamplePackageTask.doLast {
            copy {
                from file("${buildPath}/Samples/${sampleName}.unitypackage")
                into file("../samples/${sampleName}")
            }
        }

        copySamplePackageTask.ext.remoteTaskPhase = 'postbuild'
}

task import_jarresolver() {
    description = "Imports the jar resolver package into the plugin"
    ext.remoteTaskPhase = 'build'
    doFirst {
        if (file(pluginProj).exists()) {
            delete pluginProj
        }
    }
    doLast {
        def tree = fileTree("${resolverDir}")
            {
                include '*.unitypackage'
            }
        def jarresolver_package = tree.getSingleFile()

        ext.execResult = exec {
              executable "${unity_exe}"
              args "-g.building",
                   "-batchmode",
                   "-createProject", "${pluginProj}",
                   "-logFile", "build/import_resolver_unity.log",
                   "-importPackage", "${jarresolver_package}", "-quit"
              ignoreExitValue true
        }
        if (ext.execResult.exitValue != 0) {
            println "***Error Running Unity:"
            def src = file('build/import_resolver_unity.log').text
            print src
            throw new GradleException('error exporting plugin')
        }

    }

}
// don't explicitly depend on copy_jarresolver here since it is run
// as a prebuild step, and import_jarresolver is a build step which can happen
// on a machine with limited network connectivity.
// import_jarresolver.dependsOn "copy_jarresolver"

task copy_pluginProjSrc () {
    doFirst {
      copy {
        from {"${pluginBuildSrc}"}
        includes ["**/*"]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        into {"${pluginProj}"}
      }
    }
    doLast {
      copy {
        from {"${buildDir}/m2repository"}
        includes ["**/*"]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        into {"${pluginProj}/Assets/GooglePlayGames/Editor/m2repository"}
      }
    }
}
copy_pluginProjSrc.dependsOn import_jarresolver
copy_pluginProjSrc.ext.remoteTaskPhase = 'build'

task export_package () {
    description = "Creates and exports the Plugin unity package"
    doFirst {
        ext.path = "${pluginProj}/Assets/GooglePlayGames/Editor/projsettings.txt"
        delete '${ext.path}'
    }

    doLast {
        def argv = [
                "-g.building",
                // This prevents the VersionHandler to preserve the plugin layout
                "-gvh_disable",
                // NOTE: This doesn't target Android since we don't want to have
                // dependencies on the Jar Resolver as they require the plugin to
                // be enabled during the build process which would break the
                // versioning process.  Compilation is verified when exporting
                // the sample projects.
                "-batchmode",
                "-projectPath", "${pluginProj}",
                "-logFile", "build/unity.log",
                "-exportPackage",
                    "Assets/GooglePlayGames",
                    "Assets/GooglePlayGames/Plugins/Android/GooglePlayGamesManifest.plugin",
                    "Assets/PlayServicesResolver",
                    "Assets/Plugins/iOS",
                "${exportPath}",
                "-quit"
        ]

        ext.execResult = exec {
            executable "${unity_exe}"
                args argv
                ignoreExitValue true
        }
        if (ext.execResult.exitValue != 0) {
            println "***Error Running Unity:"
            def src = file('build/unity.log').text
            print src
            throw new GradleException('error exporting plugin')
        }
    }
}
export_package.dependsOn import_jarresolver
export_package.dependsOn copy_pluginProjSrc
export_package.ext.remoteTaskPhase = 'build'
/*
    Copy the plugin to the current-build directory
*/
task copy_plugin() {
    description = 'Copy plugin to the current-build directory'
    doFirst {
            copy {
                from file(exportPath)
                into file(currentPluginPath)
                rename ('plugin.unitypackage', currentPluginName)
            }
        }

    doLast {
        println "Copied  ${exportPath}  to   ${currentPluginPath}"
    }

    ext.remoteTaskPhase = 'postbuild'
}

task get_gpg_cpp_sdk () {

    doFirst {
        ant.get(src: gpg_sdk_link, dest: 'gpg_cpp_sdk.zip', skipexisting: 'true')
    }
    doLast {
        println "Got ${gpg_sdk_link} as gpg_cpp_sdk.zip"
    }
}
get_gpg_cpp_sdk.description = "Download the gpg sdk from the specified location"

get_gpg_cpp_sdk.ext.remoteTaskPhase = 'prebuild'

task check_ndk() << {
    if (ndk_root == null || ndk_root == "") {
        throw new GradleException('ANDROID_NDK_ROOT not set')
    }
    else {
        if (System.getenv("ANDROID_HOME") == null) {
            throw new GradleException('ANDROID_HOME not set')
        }
        println("OK");
    }
}
check_ndk.description = "Checks that the NDK is setup correctly"
check_ndk.ext.remoteTaskPhase = 'prebuild'


// The ndk work needs to happen in the jni directory - it is baked into the
// ndk environment.
//
task compile_gpg << {
    exec {
        workingDir "build/jni"
        executable "${ndk_root}/ndk-build"
    }
}
compile_gpg.description = "Compiles the GPG SDK libs into .so files"
compile_gpg.dependsOn 'check_ndk'
compile_gpg.dependsOn 'copy_gpg_sdk'
compile_gpg.dependsOn 'copy_native'

compile_gpg.ext.remoteTaskPhase = 'prebuild'

task copy_native << {
    copy {
        from {'native'}
        into {'build/jni'}
    }
}
copy_native.description = "Copies the native code of the plugin into the build/jni dir for compiling"
copy_native.ext.remoteTaskPhase = 'prebuild'


task copy_plugin_source << {
    copy {
        from {"${pluginSrc}"}
        into {"${pluginBuildSrc}"}
    }
}
copy_plugin_source.description = "Copies plugin source into build directory"
copy_plugin_source.ext.remoteTaskPhase = 'prebuild'


task copy_jarresolver() {
    description = "Clones the jar resolver project and copied into the build."
    ext.remoteTaskPhase = "prebuild"
    dependsOn copy_plugin_source

    doFirst {
        if (file(resolverDir).exists()) {
            delete resolverDir
        }
    }

    doLast {
        if (jarresolver_uri != null) {
            mkdir("${resolverDir}")
            def resolver = new File("${resolverDir}/resolver.unitypackage")
            new URL("${jarresolver_uri}").withInputStream {
                inputStream -> resolver.withOutputStream { it << inputStream }
            }
            return
        }

        for (repo in jarresolver_repos) {
            println 'clone ' + repo
            def result = exec {
                executable "${git_exe}"
                args "clone", repo, "${resolverDir}"
                ignoreExitValue true
            }
            if (result.exitValue == 0 && jarresolver_tag != null) {
                result = exec {
                    executable "${git_exe}"
                    args "checkout", "-b", "buildver", "${jarresolver_tag}"
                    workingDir "${resolverDir}"
                }
            }

            if (result.exitValue == 0) {
                println "Downloaded resolver from " + repo
                return
            }
        }
    }
}

task copy_gpg_sdk {
    doFirst {
        if (file('build/jni/gpg-cpp-sdk').exists()) {
            delete 'build/jni/gpg-cpp-sdk'
        }
    }
    doLast {
        copy {
            from zipTree('gpg_cpp_sdk.zip')
            // Determine the destination directory later
            into {'build/jni' }
        }
    }
}
copy_gpg_sdk.description = "Unzips the GPG SDK into the correct dir for NDK"
copy_gpg_sdk.dependsOn get_gpg_cpp_sdk
copy_gpg_sdk.ext.remoteTaskPhase = 'prebuild'

task copy_libs (dependsOn: 'compile_gpg') << {
    copy {
        from {'build/libs'}
        into {'SupportLib/PlayGamesPluginSupport/src/main/jniLibs' }
    }
}
copy_libs.description = "Copies the compiled native libs to the plugin directory"
copy_libs.dependsOn 'copy_plugin_source'
copy_libs.ext.remoteTaskPhase = 'prebuild'

task build_java_support_lib(type: GradleBuild) {
    description = "Builds the client libraries for Android"
    buildFile = 'SupportLib/build.gradle'
    startParameter.projectProperties.put('projVersion',currentPluginVersion)
    startParameter.projectProperties.put('uploadDir', "${buildDir}/m2repository")

    tasks = ['build', 'uploadArchives']
}
build_java_support_lib.ext.remoteTaskPhase = 'prebuild'
build_java_support_lib.dependsOn copy_libs

task support_jar(dependsOn:'build_java_support_lib') << {
    copy {
        from 'SupportLib/PlayGamesPluginSupport/build/outputs/aar/PlayGamesPluginSupport-debug.aar'
        into {'build/PluginSrc/Assets/Plugins/Android'}
        rename ('PlayGamesPluginSupport-debug.aar','play-games-plugin-support.aar')
    }
}
support_jar.description = "Copies the android support libs to the Plugin directory"
support_jar.dependsOn 'copy_plugin_source'

support_jar.ext.remoteTaskPhase = 'prebuild'



task clean_support_lib(type: GradleBuild) {
    description = "cleans the client libraries for Android"
    buildFile = 'SupportLib/build.gradle'
    tasks = ['clean']
}
task clean() {
    doFirst {
        delete 'build'
        }
    dependsOn clean_support_lib
}
