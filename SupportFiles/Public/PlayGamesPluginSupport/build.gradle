buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.1'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        // The following line is needed when building the plugin with an unreleased SDK.
        maven { url '/tmp/games_m2/repository' }
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

group 'com.google.games'
project.ext.baseName = "gpgs-plugin-support"

if (project.hasProperty("projVersion")) {
    project.version = project.projVersion
} else {
    project.version '0-SNAPSHOT'
}

if (project.hasProperty("uploadDir")) {
    project.ext.uploadDir = project.uploadDir
} else {
    project.ext.uploadDir = "${buildDir}/m2repository"
}

android {
    namespace 'com.google.games.bridge'

    lintOptions {
        abortOnError true
        warningsAsErrors true

        // Don't fail when new versions of dependencies are available. Allows reproducible builds
        disable("AndroidGradlePluginVersion", "GradleDependency", "NewerVersionAvailable", "UnusedResources")
    }
    defaultConfig {
        versionName  project.version
        archivesBaseName = project.ext.baseName
        minSdkVersion 21

    }
    compileSdkVersion 28
}

// Rename the .aar file to be .srcaar to work around having the local repo in a
// subdirectory in a Unity project.
// The jar resolver plugin handles the changing back from .srcaarto .aar
task renameLibrary(type: Copy) {
    from "${buildDir}/outputs/aar/${baseName}-release.aar"
    into "${buildDir}/srcaar_output"
    rename { String fn ->
        fn.replace("release.aar", "${version}.srcaar")
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        renameLibrary.dependsOn(task)
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = project.group
            artifactId = project.ext.baseName
            version = project.version

            artifact("${buildDir}/srcaar_output/${baseName}-${version}.srcaar" as File)

            // Add the required dependencies to the generated `pom.xml` file.
            pom.withXml {
                final dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    // Exclude filetree dependencies.
                    if (it.name != 'unspecified') {
                        println("Adding dependency: " + it.group + ":" + it.name + ":" + it.version)

                        final dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        if (it.artifacts.size() > 0) {
                            dependencyNode.appendNode('type', it.artifacts[0].type)
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url uri(project.ext.uploadDir)
        }
    }
}


tasks.withType(PublishToMavenRepository) { it.dependsOn renameLibrary }

dependencies {
    implementation 'com.google.android.gms:play-services-games-v2:20.1.2'
    implementation 'com.google.android.gms:play-services-nearby:18.5.0'
}
